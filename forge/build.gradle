architectury {
    platformSetupLoomIde()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
}

sourceSets.main {
    def commonSourceSets = project(":common").sourceSets

    def commonMain = commonSourceSets.named("main")

    java.srcDirs(
        commonMain.map { it.java.srcDirs },
    )

    resources.srcDirs(
        commonMain.map { it.resources.srcDirs },
    )
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    include modApi("com.teamresourceful.resourcefullib:resourcefullib-forge-$rootProject.resourcefullib_version")
    modImplementation "me.codexadrian.spirit:spirit-forge-1.19.2:${rootProject.spirit_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-forge:${rootProject.rei_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}"
    modImplementation('software.bernie.geckolib:geckolib-forge-1.19:3.1.16')
    modRuntimeOnly "mekanism:Mekanism:${mekanism_version}"// core
    modRuntimeOnly "mekanism:Mekanism:${mekanism_version}:additions"// Mekanism: Addition
    modRuntimeOnly "mekanism:Mekanism:${mekanism_version}:generators"// Mekanism: Generator
    modRuntimeOnly "mekanism:Mekanism:${mekanism_version}:tools"// Mekanism: Tool
    compileOnly(project(path: ":common")) { transitive false }
    //shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
    //modImplementation "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

jar {
    classifier "dev"
    archivesBaseName = "$archives_base_name-$name-$minecraft_version"
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
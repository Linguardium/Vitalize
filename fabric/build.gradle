import net.msrandom.postprocess.PostProcessClasses

architectury {
    platformSetupLoomIde()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

tasks.register("processJavaClasses", PostProcessClasses) {
    extensionPackages.addAll(
            "earth.terrarium.vitalize.extensions",
    )

    dependsOn(tasks.compileJava)
}

sourceSets.main {
    def commonSourceSets = project(":common").sourceSets

    def commonMain = commonSourceSets.named("main")

    java.srcDirs(
        commonMain.map { it.java.srcDirs },
    )

    resources.srcDirs(
        commonMain.map { it.resources.srcDirs },
    )

    compiledBy(processJavaClasses)
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modImplementation "me.codexadrian.spirit:spirit-fabric-1.19.2:${rootProject.spirit_version}"
    include modApi("com.teamresourceful.resourcefullib:resourcefullib-fabric-$rootProject.resourcefullib_version")
    modImplementation 'software.bernie.geckolib:geckolib-fabric-1.19:3.1.15'
    modImplementation 'RebornCore:RebornCore-1.19:+'
    modImplementation 'TechReborn:TechReborn-1.19:+'
    include modApi('teamreborn:energy:2.2.0') {
        exclude(group: "net.fabricmc.fabric-api")
    }
    compileOnly(project(path: ":common")) { transitive false }
    //shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}